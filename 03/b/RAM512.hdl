// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
        DMux8Way(
            in=load, sel=address[0..2],
            a=ram0Load, b=ram1Load, c=ram2Load, d=ram3Load,
            e=ram4Load, f=ram5Load, g=ram6Load, h=ram7Load
        );
        RAM64(in=in, load=ram0Load, address=address[3..8], out=ram0Out);
        RAM64(in=in, load=ram1Load, address=address[3..8], out=ram1Out);
        RAM64(in=in, load=ram2Load, address=address[3..8], out=ram2Out);
        RAM64(in=in, load=ram3Load, address=address[3..8], out=ram3Out);
        RAM64(in=in, load=ram4Load, address=address[3..8], out=ram4Out);
        RAM64(in=in, load=ram5Load, address=address[3..8], out=ram5Out);
        RAM64(in=in, load=ram6Load, address=address[3..8], out=ram6Out);
        RAM64(in=in, load=ram7Load, address=address[3..8], out=ram7Out);

        Mux8Way16(
            a=ram0Out, b=ram1Out, c=ram2Out, d=ram3Out,
            e=ram4Out, f=ram5Out, g=ram6Out, h=ram7Out,
            sel=address[0..2], out=out
        );
}